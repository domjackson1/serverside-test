buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.1.RELEASE")
    }
}

plugins {
    id "com.palantir.docker" version "0.19.2"
}

group = 'domjackson1'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

bootJar {
    baseName = 'serverside-test'
    version =  '0.1.0'
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

docker {
    name "${project.group}/${jar.baseName}:${jar.version}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'hello', 'src/test/resources']
        }
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.jsoup:jsoup:1.11.3'
    compile 'org.apache.logging.log4j:log4j-api:2.11.0'
    compile 'org.apache.logging.log4j:log4j-core:2.11.0'
    compile 'commons-validator:commons-validator:1.4.0'
    compile 'com.fasterxml:jackson-module-json-org:0.9.1'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'io.cucumber:cucumber-java8:2.4.0'
    testCompile 'io.cucumber:cucumber-junit:2.4.0'
    testCompile 'info.cukes:cucumber-spring:1.2.4'
}

